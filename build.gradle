import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'de.undercouch.download' version '4.1.2'
}

apply from: 'gradle/publishing.gradle'

repositories {
    jcenter()
    mavenCentral()
}

wrapper {
    gradleVersion = '7.5.1'
}

sourceCompatibility = 11
targetCompatibility = 11
tasks.withType(JavaCompile) {
    options.release.set(11)
    options*.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
}

task downloadVMF(type: Download) {
    src 'https://repo1.maven.org/maven2/eu/mihosoft/vmf/vmf/0.2.8.6/vmf-0.2.8.6.jar'
    dest buildDir
    overwrite false
}

test.dependsOn+="downloadVMF"


dependencies {
    testImplementation 'junit:junit:4.13.2'
    implementation group: 'io.github.classgraph', name: 'classgraph', version: '4.8.149'
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'eu.mihosoft.jcompiler')
    }
}

license {
    header = rootProject.file('config/HEADER')

    strictCheck = true
    ignoreFailures = false

    mapping {
        java   = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
        gradle = 'SLASHSTAR_STYLE'
        fxml   = 'XML_STYLE'
        html   = 'XML_STYLE'
    }

    ext.yearCurrent  = new SimpleDateFormat("yyyy").format(new Date())
    ext.years  = ('2020'.equals(ext.yearCurrent)?'':'2020-')+ext.yearCurrent
    ext.author1 = 'Michael Hoffer <info@michaelhoffer.de>'
}
